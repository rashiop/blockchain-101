# ERC721 standard and Crypto-collectible assets
Token on ETH is a smart contract that follows common rules:
- transferFrom(address _from, address _to, uint256 _tokenId)
- balanceOf(address _owner)

<details>
<summary>CODE</summary>

zombieownership.sol
  ```sol
    pragma solidity >=0.5.0 <0.6.0;

    import "./zombieattack.sol";
    import "./erc721.sol";
    import "./safemath.sol";

    /// TODO: Replace this with natspec descriptions
    contract ZombieOwnership is ZombieAttack, ERC721 {

      using SafeMath for uint256;

      mapping (uint => address) zombieApprovals;

      function balanceOf(address _owner) external view returns (uint256) {
        return ownerZombieCount[_owner];
      }

      function ownerOf(uint256 _tokenId) external view returns (address) {
        return zombieToOwner[_tokenId];
      }

      function _transfer(address _from, address _to, uint256 _tokenId) private {
        ownerZombieCount[_to] = ownerZombieCount[_to].add(1);
        ownerZombieCount[msg.sender] = ownerZombieCount[msg.sender].sub(1);
        zombieToOwner[_tokenId] = _to;
        emit Transfer(_from, _to, _tokenId);
      }

      function transferFrom(address _from, address _to, uint256 _tokenId) external payable {
        require (zombieToOwner[_tokenId] == msg.sender || zombieApprovals[_tokenId] == msg.sender);
        _transfer(_from, _to, _tokenId);
      }

      function approve(address _approved, uint256 _tokenId) external payable onlyOwnerOf(_tokenId) {
        zombieApprovals[_tokenId] = _approved;
        emit Approval(msg.sender, _approved, _tokenId);
      }

    }
  ```

  erc721.sol
  ```sol
    contract ERC721 {
      event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);
      event Approval(address indexed _owner, address indexed _approved, uint256 indexed _tokenId);

      function balanceOf(address _owner) external view returns (uint256);
      function ownerOf(uint256 _tokenId) external view returns (address);
      function transferFrom(address _from, address _to, uint256 _tokenId) external payable;
      function approve(address _approved, uint256 _tokenId) external payable;
    }
  ```

</details>

## Token standard
<details>
  <summary>Why does it matter? ERC20</summary>
  Since all `ERC20 tokens` share the same set of functions with the same names, they can all be interacted with in the same ways.

  If we build an application that can interacting with one ERC20 token, it's also capable of interacting with any ERC20 token without needing to be custom coded. 
  
  You could simply plug in the new token contract address, and boom, your app has another token it can use.
</details>

<details>
  <summary>ERC21 - crypto-collectibles</summary>
  Sometimes not all token created equally like in collectibles case. The token standard called `ERC721 tokens`<br /><br />

  Steps:
  - we need to implement on our local
    ```sol
      contract ERC721 {
        event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);
        event Approval(address indexed _owner, address indexed _approved, uint256 indexed _tokenId);

        function balanceOf(address _owner) external view returns (uint256);
        function ownerOf(uint256 _tokenId) external view returns (address);
        function transferFrom(address _from, address _to, uint256 _tokenId) external payable;
        function approve(address _approved, uint256 _tokenId) external payable;
      }
    ```
  - inherit from ERC721
      - solidity handle multiple inheritance
</details>

## Transfer logic
Note that the ERC721 spec has 2 different ways to transfer tokens:

```sol
function transferFrom(
  address _from,
  address _to,
  uint256 _tokenId
) external payable;
```

The first way is the token's owner calls transferFrom with his address as the _from parameter, the address he wants to transfer to as the _to parameter, and the _tokenId of the token he wants to transfer.

```sol
function approve(address _approved, uint256 _tokenId) external payable;
function transferFrom(
  address _from,
  address _to,
  uint256 _tokenId
) external payable;
```

The second way is the token's owner first calls approve with the address he wants to transfer to, and the _tokenID . The contract then stores who is approved to take a token, usually in a mapping (uint256 => address). Then, when the owner or the approved address calls transferFrom, the contract checks if that msg.sender is the owner or is approved by the owner to take the token, and if so it transfers the token to him.

Notice that both methods contain the same transfer logic. In one case the sender of the token calls the transferFrom function; in the other the owner or the approved receiver of the token calls it.

So it makes sense for us to abstract this logic into its own private function, _transfer, which is then called by transferFrom.

## Preventing Overflow
We need to make sure users does not accidentally transfer their zombies to address 0 (aka burning a token aka sent to an address that no one has the private key of -> unrecoverable)

### Contracts security enhancements: Overflows and Underflows
- `Overflow` more than the digit assigned
- `Underflow` negative digits on uint

### Using `SafeMath`
OpenZeppelin Library, usage:
```sol
using SafeMath for uint256;

uint256 a = 5;
uint256 b = a.add(3); // 5 + 3 = 8
uint256 c = a.mul(2); // 5 * 2 = 10

```

## Comment
- //
- /**/
- natspec
    `@title` and `@author` are straightforward.

    `@notice` explains to a user what the contract / function does. `@dev` is for explaining extra details to developers.

    `@param` and `@return` are for describing what each parameter and return value of a function are for.

    Note that you don't always have to use all of these tags for every function â€” all tags are optional. But at the very least, leave a `@dev` note explaining what each function does.
    
    ```sol
    /// @title A contract for basic math operations
    /// @author H4XF13LD MORRIS ðŸ’¯ðŸ’¯ðŸ˜ŽðŸ’¯ðŸ’¯
    /// @notice For now, this contract just adds a multiply function
    contract Math {
      /// @notice Multiplies 2 numbers together
      /// @param x the first uint.
      /// @param y the second uint.
      /// @return z the product of (x * y)
      /// @dev This function does not currently check for overflows
      function multiply(uint x, uint y) returns (uint z) {
        // This is just a normal comment, and won't get picked up by natspec
        z = x * y;
      }
    }
    ```



